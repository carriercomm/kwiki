#!/usr/bin/env perl
use 5.008003;
use strict;
use warnings;
our $VERSION = '0.40';
use Cwd 'cwd', 'abs_path';
use FindBin;
use Config;

sub find_base {
    return $ENV{KWIKI_BASE}
      if defined $ENV{KWIKI_BASE};

    my $base = $FindBin::Bin;
    my $bin = $0;
    $bin =~ s/.*\///;
    $bin = "$base/$bin";
    my $home = cwd();
    while (-l $bin) {
        chdir $base;
        $bin = abs_path(readlink $bin);
        $base = $bin;
        $base =~ s/(.*)\/.*/$1/;
    }
    chdir $home;
    $base = abs_path("$base/../..");
    return $base if -d "$base/lib/Kwiki" and -d "$base/core/Kwiki";

    if ($ENV{HOME}) {
        $base = abs_path("$ENV{HOME}/.kwiki");
        return $base
          if (-d $base and -w $base);
    }

    die <<'...';

*ERROR*: Can't locate Kwiki base repository.

Try setting the KWIKI_BASE environment variable to a writable directory
where Kwiki can store site information. Alternately you can create a
~/.kwiki/ directory for this purpose.

If you install Kwiki from SVN (http://svn.2.kwiki.org/kwiki/trunk/) or
from a Kwiki download tarball (instead of CPAN), then Kwiki will use
that directory as the base.

NOTE: Installing Kwiki from SVN is recommended. It makes installing and
      updating Kwiki trivial.

...
} 

# Add $KWIKI_BASE/lib to @INC;
BEGIN {
    $ENV{KWIKI_TEST} = 1;
    if ($ENV{KWIKI_TEST}) {
        @INC = (
            $Config::Config{privlib},
            $Config::Config{archlib},
        ); 
    }

    my $base = find_base();
    if (-e "$base/lib") {
        eval "use lib '$base/lib'; 1"
          or die $@;
    }
    $ENV{KWIKI_BASE} = $base;
}

use Kwiki;
my @configs = qw(config*.yaml -plugins plugins);
Kwiki->new->load_hub(@configs)->command->process(@ARGV)->hub->remove_hooks;

__END__

=head1 NAME

kwiki - The Kwiki Command Line Tool

=head1 USAGE

    > kwiki -new /path/to/cgi-bin/my-kwiki

    Kwiki software installed! Point your browser at this location.

=head1 DESCRIPTION

Kwiki is a simple extendable wiki framework, written in Perl.

See L<Kwiki::Command> for more information on using this command line tool.

=head1 NOTE

If you are on Mac OS X, use the command C<perldoc Kwiki.pm> to get the
Kwiki B<module> documentation.

=head1 AUTHOR

Ingy döt Net <ingy@cpan.org>

=head1 COPYRIGHT

Copyright (c) 2004-2005. Brian Ingerson. All rights reserved.

Copyright (c) 2006. Ingy döt Net. All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

See http://www.perl.com/perl/misc/Artistic.html

=cut

# vim: set ft=perl:
